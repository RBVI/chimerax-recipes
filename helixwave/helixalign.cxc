# Open extended helix conformation
open 6j0b

# Delete needle and one of the two stacked rings
delete /G-L,a-l

# Recenter at the origin
measure center #1
# Output: Center of mass of 16476 atoms = (143.53, 143.53, 134.14)
move -143.53,-143.53,-134.14 model #1 coord #1

# Open contracted helix conformation
open 6j0c

# Delete one of the two helix rings
del #2/a-f

# 6j0b and 6j0c have chain identifiers A-F going in opposite directions around helix.
# So reassign the chain identifiers of 6j0c.
changechain #2 A,B,C,D,E,F F,E,D,C,B,A

# Align 6j0c to 6j0b
matchmaker #2/A,B,C,D,E,F to #1/A,B,C,D,E,F pair ss cutoff none

# Create morph and save
morph #1,2
save 6j0b_6j0c_morph.pdb model #3 allCoords true

# Create needle structure from 6j0b chains a-f
open 6j0b
align #4/A-F to #1/A-F
del #4/A-L,g-l
sym #4 H,39.3,19.9,28 copies true
save needle.cif model #5
